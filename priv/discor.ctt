-- either A B is discrete if A and B are
module discor where

import proto
import prop

inlNotinr (A B:U) (a:A) (b:B) (h: Path (either A B) (inl a) (inr b)) : Empty =
 subst (either A B) T (inl a) (inr b) h tt
 where
  T : either A B -> U = split
       inl _ -> Unit
       inr _ -> Empty

inrNotinl (A B:U) (a:A) (b:B) (h : Path (either A B) (inr b) (inl a)) : Empty =
 subst (either A B) T (inr b) (inl a) h tt
 where
  T : either A B -> U = split
       inl _ -> Empty
       inr _ -> Unit

injInl (A B :U) (x0 x1:A) (h : Path (either A B) (inl x0) (inl x1)) : Path A x0 x1 =
 subst (either A B) T (inl x0) (inl x1) h (refl A x0)
 where
   T : either A B -> U = split
         inl x -> Path A x0 x
         inr _ -> Empty

injInr (A B :U) (x0 x1:B) (h: Path (either A B) (inr x0) (inr x1)) : Path B x0 x1 =
 subst (either A B) T (inr x0) (inr x1) h (refl B x0)
 where
   T : either A B -> U = split
         inl _ -> Empty
         inr x -> Path B x0 x

-- If A and B are discrete then "A either B" is discrete
orDisc (A B : U) (dA : discrete A) (dB : discrete B) :
  (z z1 : either A B) -> dec (Path (either A B) z z1) = split
  inl a -> rem1
    where rem1 : (z1:either A B) -> dec (Path (either A B) (inl a) z1) = split
            inl a1 -> rem (dA a a1)
               where rem : dec (Path A a a1) -> dec (Path (either A B) (inl a) (inl a1)) = split
                            inl p -> inl (<i> inl (p @ i))
                            inr h -> inr (\ (p:Path (either A B) (inl a) (inl a1)) -> h (injInl A B a a1 p))
            inr b -> inr (inlNotinr A B a b)
  inr b -> rem1
   where rem1 : (z1:either A B) -> dec (Path (either A B) (inr b) z1) = split
            inl a -> inr (inrNotinl A B a b)
            inr b1 -> rem (dB b b1)
               where rem : dec (Path B b b1) -> dec (Path (either A B) (inr b) (inr b1)) = split
                            inl p -> inl (<i> inr (p @ i))
                            inr h -> inr (\ (p:Path (either A B) (inr b) (inr b1)) -> h (injInr A B b b1 p))

