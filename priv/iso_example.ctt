module iso_example where

import nat
import iso

data nat2 = zero2 | suc2 (n : nat2)

PathTypes (A B: U)                                  : U = PathP (<i> U) A B
PathElem  (A B: U) (a: A) (b: B) (P: PathTypes A B) : U = PathP P a b

toNat : nat2 -> nat = split
  zero2 -> zero
  suc2 n -> succ (toNat n)

fromNat : nat -> nat2 = split
  zero -> zero2
  succ n -> suc2 (fromNat n)

toNatK : (n : nat2) -> Path nat2 (fromNat (toNat n)) n = split
  zero2 -> <_> zero2
  suc2 n -> <i> suc2 (toNatK n @ i)

fromNatK : (n : nat) -> Path nat (toNat (fromNat n)) n = split
  zero -> <_> zero
  succ n -> <i> succ (fromNatK n @ i)

nat2nat : Path U nat2 nat = isoPath nat2 nat toNat fromNat fromNatK toNatK
