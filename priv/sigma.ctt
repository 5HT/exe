{- Sigma Type:
   Copyright (c) Groupoid Infinity, 2014-2018.

   see HoTT 2.7 Sigma-types
   see HoTT 1.6 Dependent pair types (Sigma-types)
-}

module sigma where

import set

Sigma (A : U) (B : A -> U) : U = (x : A) * B x
exists (A: U) (B: A -> U) (a : A) (b: B a): Sigma A B = (a,b)
pr1  (A: U) (B: A -> U) (x: Sigma A B): A = x.1
pr2  (A: U) (B: A -> U) (x: Sigma A B): B (pr1 A B x) = x.2

sigInd (A: U) (B: A -> U) (C: Sigma A B -> U) (g: (a: A) (b: B a) -> C (a, b)) (p: Sigma A B): C p
  = g p.1 p.2

sigma_model (A: U) : U
    = (_: (B: A -> U) (a: A) (b: B a) -> Path A (pr1 A B (exists A B a b)) a)
    * ((B: A -> U) (a: A) (b: B a) -> Path (B a) (pr2 A B (exists A B a b)) b)

pathSig0 (A:U) (B:A-> U) (t u : Sigma A B) (p:Path A t.1 u.1)
  : Path U (PathP (<i>B (p@i)) t.2 u.2) (Path (B u.1) (transport (<i>B (p@i)) t.2) u.2)
  = funDepTr A B t.1 u.1 p  t.2 u.2 where
  funDepTr (A:U) (P:A->U) (a0 a1 :A) (p:Path A a0 a1) (u0:P a0) (u1:P a1)
    : Path U (PathP (<i> P (p@i)) u0 u1) (Path (P a1) (transport (<i> P (p@i)) u0) u1)
    = <j>PathP (<i>P (p@j\/i)) (comp (<i>P (p@j/\i)) u0 [(j=0)-><_>u0]) u1

corSigProp (A:U) (B:A-> U) (pB : (x:A) -> isProp (B x)) (t u : Sigma A B) (p:Path A t.1 u.1) :
  isProp (PathP (<i>B (p@i)) t.2 u.2) = substInv U isProp T0 T1 rem rem1
 where P : Path U (B t.1) (B u.1) = <i>B (p@i)
       T0 : U = PathP P t.2 u.2
       T1 : U = Path (B u.1) (transport P t.2) u.2
       rem : Path U T0 T1 = pathSig0 A B t u p -- funDepTr (B t.1) (B u.1) P t.2 u.2
       v2 : B u.1 = transport P t.2
       rem1 : isProp T1 = propSet (B u.1) (pB u.1) v2 u.2

corSigSet (A:U) (B:A-> U) (sB : (x:A) -> isSet (B x)) (t u : Sigma A B) (p:Path A t.1 u.1) :
  isProp (PathP (<i>B (p@i)) t.2 u.2) = substInv U isProp T0 T1 rem rem1
 where P : Path U (B t.1) (B u.1) = <i>B (p@i)
       T0 : U = PathP P t.2 u.2
       T1 : U = Path (B u.1) (transport P t.2) u.2
       rem : Path U T0 T1 = pathSig0 A B t u p -- funDepTr (B t.1) (B u.1) P t.2 u.2
       v2 : B u.1 = transport P t.2
       rem1 : isProp T1 = sB u.1 v2 u.2

sigSecondPath (A: U) (P: A -> U) (t u: Sigma A P) (p: Path A t.1 u.1): U
  = Path (P u.1) (transport p' t.2) u.2 where
  p' : Path U (P t.1) (P u.1) = mapOnPath A U P t.1 u.1 p

{-
    Church encoded pure sigma ported from Om.

sig (A:U)(P:A->U)(x:A): U                         = (e:U)->(i:A->P x->e)->e
mk  (A:U)(P:A->U)(x:A)(y:P x)(e:U)(i:A->P x->e):e = i x y
fst (A:U)(P:A->U)(x:A)(s:sig A P x):A             = s A     (\(z:A)(y:P x)->z)
snd (A:U)(P:A->U)(x:A)(s:sig A P x):P x           = s (P x) (\(z:A)(y:P x)->y)

-- import list
-- P: nat -> U = (\(_:nat) -> list nat)
-- test: sig nat P n5 = mk nat P n5 nil
-- test_pr1: nat = fst nat P zero test
-- test_pr2: list nat = snd nat P zero test

-}

